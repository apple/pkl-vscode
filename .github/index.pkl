//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//

amends "@pkl.impl.ghactions/PklCI.pkl"

import "@gha/actions/Artifact.pkl"
import "@gha/actions/Common.pkl"
import "@gha/actions/Setup.pkl"
import "@gha/Context.pkl"
import "@gha/Workflow.pkl"
import "@pkl.impl.ghactions/jobs/HawkeyeCheck.pkl"

local emsdkVersion = "2.0.24"
local pklVersion = "0.30.0"

local buildArtifactsName = "pkl-vscode-build-artifacts"

triggerDocsBuild = "release"

local buildAndTest: Workflow = new {
  jobs {
    ["build"] {
      `runs-on` = "ubuntu-latest"
      steps {
        new Common.Checkout {
          with {
            submodules = "recursive"
          }
        }
        new Setup.Node {
          with {
            `node-version` = "24.x"
          }
        }
        new {
          name = "Install emsdk"
          run =
            #"""
            git clone https://github.com/emscripten-core/emsdk
            cd emsdk
            ./emsdk install \#(emsdkVersion)
            ./emsdk activate \#(emsdkVersion)
            echo "${GITHUB_WORKSPACE}/emsdk" >> $GITHUB_PATH
            echo "${GITHUB_WORKSPACE}/emsdk/upstream/emscripten" >> $GITHUB_PATH
            """#
        }
        new {
          name = "Set up Pkl"
          run =
            #"""
            mkdir /tmp/pkl
            curl -L "https://github.com/apple/pkl/releases/download/\#(pklVersion)/pkl-linux-amd64" -o /tmp/pkl/pkl
            chmod +x /tmp/pkl/pkl
            echo '/tmp/pkl' >> $GITHUB_PATH
            """#
        }
        new { run = "npm ci" }
        new { run = "npm run build" }
        new { run = "npm test" }
        new { run = "npm run lint" }
        new { run = "npm run package-only" }
        new Artifact.Upload {
          with {
            name = buildArtifactsName
            path = ".dist/vscode/*.*"
          }
        }
      }
    }
    ["test-format-license-headers"] = new HawkeyeCheck {}.job
  }
}

prb = buildAndTest
build = buildAndTest
main = buildAndTest
releaseBranch = buildAndTest

release = (buildAndTest) {
  jobs {
    ["deploy-github-release"] {
      `runs-on` = "ubuntu-latest"
      needs = buildAndTest.jobs.keys.toListing()
      permissions {
        contents = "write"
      }
      steps {
        new Artifact.Download {
          with {
            name = buildArtifactsName
            path = ".dist/vscode/"
          }
        }
        new {
          name = "Publish release to GitHub"
          env {
            ["GH_TOKEN"] = Context.github.token
            ["GH_REPO"] = Context.github.repository
          }
          run =
            #"""
            gh release create "\#(Context.github.refName)" \
              --title "\#(Context.github.refName)" \
              --target "\#(Context.github.sha)" \
              --verify-tag \
              --notes "Release notes: https://pkl-lang.org/vscode/current/changelog.html#release-\#(Context.github.refName)" \
              .dist/vscode/*.vsix
            """#
        }
      }
    }
  }
}
