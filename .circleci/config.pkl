amends "package://pkg.pkl-lang.org/pkl-project-commons/pkl.impl.circleci@1.2.0#/PklCI.pkl"

local emsdkVersion = "2.0.24"

prb = buildWorkflow

main = buildWorkflow

triggerDocsBuild = "release"

release = (buildWorkflow) {
  jobs {
    new {
      ["do-release"] {
        requires {
          "test-license-headers"
          "build"
        }
        context = "pkl-github-release"
      }
    }
  }
}

jobs {
  ["test-license-headers"] {
    docker {
      new {
        image = "ghcr.io/korandoru/hawkeye"
      }
    }
    steps {
      "checkout"
      new RunStep {
        command = "/bin/hawkeye check --fail-if-unknown"
      }
    }
  }
  ["build"] {
    docker {
      new {
        image = "cimg/node:24.11.0"
      }
    }
    steps {
      "checkout"
      new RunStep {
        name = "Install emsdk"
        command = #"""
          git clone https://github.com/emscripten-core/emsdk
          cd emsdk
          ./emsdk install \#(emsdkVersion)
          ./emsdk activate \#(emsdkVersion)
          echo 'export PATH=${PWD}/emsdk:${PWD}/emsdk/upstream/emscripten:${PATH}' >> $BASH_ENV
          """#
      }
      new RunStep {
        name = "Download Pkl"
        command = #"""
          mkdir /tmp/pkl
          curl -L "https://github.com/apple/pkl/releases/download/0.25.1/pkl-linux-amd64" > /tmp/pkl/pkl
          chmod +x /tmp/pkl/pkl
          echo 'export PATH=/tmp/pkl:${PATH}' >> $BASH_ENV
          """#
      }
      new RunStep { command = "git submodule update --init --recursive" }
      new RunStep { command = "npm ci" }
      new RunStep { command = "npm run build" }
      new RunStep { command = "npm test" }
      new RunStep { command = "npm run lint" }
      new RunStep { command = "npm run package-only" }
      new PersistToWorkspaceStep {
        root = "."
        paths {
          ".dist/vscode/*.*"
        }
      }
    }
  }
  ["do-release"] {
    docker {
      new { image = "maniator/gh:v2.40.1" }
    }
    steps {
      new AttachWorkspaceStep { at = "." }
      new RunStep {
        name = "gh release"
        //language=bash
        command = #"""
          echo "Creating release"
          gh release create ${CIRCLE_TAG} \
            --title "${CIRCLE_TAG}" \
            --target "${CIRCLE_SHA1}" \
            --verify-tag \
            --notes "Release notes: https://pkl-lang.org/vscode/current/changelog.html#release-${CIRCLE_TAG}" \
            --repo "${CIRCLE_PROJECT_USERNAME}/${CIRCLE_PROJECT_REPONAME}" \
            .dist/vscode/*.vsix
          """#
      }
    }
  }
}

local buildWorkflow = new Workflow {
  jobs {
    "test-license-headers"
    "build"
  }
}
